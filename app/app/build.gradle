plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

// local.properties load
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def kakao_apiKey = properties.getProperty('kakao.api_key') // get kakao api key
def kakao_apiWebKey = properties.getProperty('kakao.web_api_key')
def server_url = properties.getProperty('server.url')

android {
    namespace 'com.comet.letseat'
    compileSdk 34

    defaultConfig {
        applicationId "com.comet.letseat"
        minSdk 28
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [API_KEY: kakao_apiKey] //Holder가 아니라 holder..

        buildConfigField "String", "API_KEY", "\"" + kakao_apiKey + "\"" // sdk init 용. local properties에는 따옴표가 없기때문에 붙여줌
        buildConfigField "String", "WEB_API_KEY", kakao_apiWebKey // rest 요청에 필요한 web api key
        buildConfigField "String", "SERVER_URL", server_url // server url

        // dlopen failed: library "libK3fAndroid.so" not found 해결용
        ndk {
            abiFilters.add("arm64-v8a")
            abiFilters.add("armeabi-v7a")
            abiFilters.add("x86")
            abiFilters.add("x86_64")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // permissionx
    implementation 'com.guolindev.permissionx:permissionx:1.8.1'

    // kakao map
    implementation 'com.kakao.maps.open:android:2.9.5'

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // sandwich
    implementation "com.github.skydoves:sandwich:1.2.4"

    // datastore
    implementation "androidx.datastore:datastore-preferences:1.1.0"

    // mockk
    testImplementation "io.mockk:mockk:1.13.10"
    // coroutine test
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}